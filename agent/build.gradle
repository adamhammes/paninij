plugins {
  id 'base'
}

def langJar = project(':core:lang').jar.archivePath
def procJar = project(':core:proc').jar.archivePath

task makeBuildDir << {
  buildDir.mkdirs()
}

task makefiles(type: Exec, dependsOn: makeBuildDir) {
  description = 'Uses CMake to initialize the build directory.'
  workingDir buildDir
  commandLine 'cmake', '-G', 'Unix Makefiles',
                       '-D', "PANINIJ_LANG_JAR=${langJar}",
                       '-D', "PANINIJ_PROC_JAR=${procJar}",
                       '-D', 'CMAKE_BUILD_TYPE=Debug', projectDir
}

task makeAll(type: Exec, dependsOn: ['makefiles', ':core:lang:build']) {
  workingDir buildDir
  commandLine 'make', 'all'
}
build.dependsOn makeAll

task makeTest(type: Exec, dependsOn: makefiles) {
  workingDir buildDir
  commandLine 'make', 'test'
}
check.dependsOn makeTest


// CMake recommends that a subdirectory of the source directory not be used.
def eclipseBuildDir = file("$projectDir/../agent-eclipse")

task makeEclipseBuildDir << {
  eclipseBuildDir.mkdirs()
}

task eclipse(type: Exec, dependsOn: makeEclipseBuildDir) {
  description = 'Uses CMake to initialize an Eclipse project.'
  workingDir eclipseBuildDir
  commandLine 'cmake', '-G', 'Eclipse CDT4 - Unix Makefiles',
                       '-D', "PANINIJ_LANG_JAR=${langJar}",
                       '-D', "PANINIJ_PROC_JAR=${procJar}",
                       '-D', 'CMAKE_BUILD_TYPE=Debug', projectDir
}

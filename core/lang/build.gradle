apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'eclipse'
apply plugin: 'idea'

description '''
Classes which are part of the common @PaniniJ runtime. These are needed to
compile and/or run any @PaniniJ program.
'''

modifyPom {
  project {
    artifactId 'lang'
    description project.description
    name '@PaniniJ Language and Runtime'
  }
}

dependencies {
  testCompile 'junit:junit:4.12'
}

javadoc.options.tags << 'spec:a:"spec:"'  // `@spec` is used in `String`
javadoc.exclude 'org/paninij/runtime/'    // `runtime` is not public API

// Nothing is excluded in this alternative Javadoc task. For the `check`
// task to succeed, this Javadoc compilation must not fail.
task javadocPrivate(type: Javadoc) {
  source = sourceSets.main.allJava
  destinationDir = reporting.file("javadoc-private")
  options.tags << 'spec:a:"spec:"'  // `@spec` is used in `String`
}
check.dependsOn javadocPrivate
